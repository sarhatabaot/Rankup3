import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id "io.freefair.lombok" version "6.3.0"
    id "maven-publish"
}

group 'sh.okx'
version '3.13.2'

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url 'https://raw.github.com/PyvesB/AdvancedAchievements/mvn-repo/'
    }
    maven {
        url 'https://repo.aikar.co/content/groups/aikar/'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    testImplementation 'org.junit.platform:junit-platform-launcher:1.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'com.github.MockBukkit:MockBukkit:04889261630cd6f5aaebd86a576bbcd12c442ea7'

    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly('com.github.Realizedd:TokenManager:3.2.4') {
        transitive = false
    }

    compileOnly('com.github.MilkBowl:VaultAPI:1.7.1') {
        exclude group: 'org.bukkit'
    }
    compileOnly ('me.clip:placeholderapi:2.11.0') {
        exclude group: 'org.bstats'
    }
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'

    compileOnly 'com.github.pyvesb:advanced-achievements:6.7.2'
    compileOnly 'com.github.astei:Superbvote:700fca43659b438cb9bb36c218a7646d2f2ef315'
    compileOnly('com.github.mcMMO-Dev:mcMMO:601297') {
        exclude group: 'com.sk89q.worldguard'
    }
    compileOnly 'com.github.BenCodez:VotingPlugin:6.0'
    compileOnly 'com.github.LlmDl:Towny:25fc18a'

    implementation ('io.pebbletemplates:pebble:3.1.5') {
        exclude group: 'org.slf4j'
    }
    implementation 'com.electronwill.night-config:toml:3.6.4'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.github.tr7zw.Item-NBT-API:item-nbt-api-plugin:2.9.0'
}

artifacts {
    archives shadowJar
}

shadowJar {
    archiveClassifier.set('')
    minimize()
    relocate 'org.slf4j', 'sh.okx.rankup.export.org.slf4j'
    relocate 'org.bstats', 'sh.okx.rankup.metrics'
    relocate 'co.aikar.commands', 'sh.okx.rankup.acf'
    relocate 'co.aikar.locales', 'sh.okx.rankup.locales'
    relocate 'de.tr7zw','sh.okx.rankup.nbt'
}

// automatically copy the version to plugin.yml
processResources {
    // do this again whenever version changes
    inputs.property 'version', project.version
    duplicatesStrategy = 'include'

    // copy plugin.yml and replace version
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }

    // copy everything else
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

task spigot(type: ShadowJar) {
    from sourceSets.main.runtimeClasspath
    destinationDirectory = file("./spigot/plugins/")
    archiveFileName = "Rankup.jar"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

